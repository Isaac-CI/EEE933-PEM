go athena
#
# Set up a mesh suitable for a single MOSFET device....
line x loc=0 spac=0.1 
line x loc=0.35 spac=0.02
line x loc=0.6 spac=0.1
#
line y loc=0.00 spac=0.005 
line y loc=0.3 spac=0.015
line y loc=0.5 spac=0.02
line y loc=2 spac=0.2 
line y loc=5 spac=1 
#
# Start off by defining silicon with 1e14 phos doping...
# Decrease the folowing space.mult parameter for a denser
# mesh and more accuracy...
init orientation=100 c.boron=1e14 space.mult=2
# 
#pwell formation including masking off of the nwell
#
diffus time=30 temp=1000 dryo2 press=1.00 hcl=3
#
etch oxide thick=0.02
#
#N-well Implant
implant amorphous phos dose=3e12 energy=180 pears
#
diffus temp=950 time=100 weto2 hcl=3
#
#N-well implant amorphous not shown -
#
# welldrive 
diffus time=240 temp=1200 nitro press=1
#
etch oxide all
#
#sacrificial "cleaning" oxide
diffus time=20 temp=1000 dryo2 press=1 hcl=3
#
etch oxide all
#
#gate oxide grown here:-
set partial_press=1.0
diffus time=10 temp=800 nitrogen hcl=3
diffus time=2 temp=900 dryo2 press=$partial_press
diffus time=13 temp=900 dryo2 press=$partial_press hcl=3
diffus time=10 temp=900 nitrogen hcl=3
diffus time=10 temp=800 t.final=800 nit hcl=3
#
# Extract a design parameter.....
extract name="gateox" thickness oxide mat.occno=1 x.val=0.005
#
#vt adjust implant amorphous 
implant amorphous phos dose=1.8e13 energy=25 pearson
#
depo poly thick=0.250 div=3 
#
etch poly left p1.x=0.35
#
# Relax the mesh below the 0.5um plane, for speed....
relax y.min=0.5
#
method fermi compress
diffuse time=5 temp=900 weto2 press=0.8
#
# PLDD implant amorphous
implant amorphous bf2 dose=2.0e14 energy=50 pearson
#
# This is a good way of defining the spacer.....define a variable
# first with the 'set' command....
set spacer=0.15
depo oxide thick=$"spacer" divisions=5
etch oxide dry thick=$"spacer"+0.005
#
# P++ Implant
implant amorphous bf2 dose=1.50e15 energy=60 pearson
#
#
# Final anneal. 
method fermi compress
diffuse time=5 temp=900 nitro press=1.0
#


# Etch contact cuts and place the aluminium where electrodes are required.....
etch oxide left p1.x=0.15
deposit alumin thick=0.03 div=2
etch alumin right p1.x=0.16


# mirror the structure.....
structure mirror right



# Name the electrodes...
electrode name=gate x=0.5 
electrode name=source x=0
electrode name=drain x=1.1
electrode name=substrate backside


# output the structure
structure outfile=mos1ex08_0.str 

# plot it
tonyplot  mos1ex08_0.str -set  mos1ex08_0.set


############# PVt Test : Returns PVt, PBeta and PTheta ################
go atlas

# set material models
models cvt srh print  
contact name=gate p.poly
interface qf=3e10

# get initial solution
solve init


method  newton trap
solve prev

# Bias the drain a bit...
solve vdrain=-0.1 name=drain

# Ramp the gate
log outf=mos1ex08_1.log master
solve vgate=0 vstep=-0.25 vfinal=-5.0 vdrain=-0.1 name=gate
save outf=mos1ex08_1.str

# extract device parameters......

go atlas
extract init infile="mos1ex08_1.log"

extract name="pvt" (xintercept(maxslope(curve(abs(v."gate"),abs(i."drain")))) - abs(ave(v."drain"))/2.0)

# plot results
tonyplot   mos1ex08_1.log -set mos1ex08_1_log.set

quit
