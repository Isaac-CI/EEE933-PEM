go athena

# Mesh (aumentado em torno de 0.35 pq a oxidação estava quebrando)
line x loc=0 spac=0.1 
line x loc=0.35 spac=0.02
line x loc=0.6 spac=0.1

line y loc=0.00 spac=0.005 
line y loc=0.3 spac=0.015
line y loc=0.5 spac=0.02
line y loc=2 spac=0.2 
line y loc=5 spac=1 

#Inicia com umm substrato P

init orientation=100 c.boron=1e14 space.mult=2

#Formação do óxido inicial de proteção para o implante

diffus time=30 temp=1000 dryo2 press=1.00 hcl=3

etch oxide thick=0.02

# Implante do N-well
implant amorphous phos dose=3e12 energy=180 pears

#óxido de proteção
diffus temp=950 time=100 weto2 hcl=3

# Difusão para o N-well
diffus time=240 temp=1200 nitro press=1

etch oxide all

# Crescimento do óxido de "limpeza"
diffus time=220 temp=1000 dryo2 press=1 hcl=3

etch oxide all

#--- AJUSTE DO Vt (crescimento do óxido de gate)
set partial_press=1.0
diffus time=10 temp=800 nitrogen hcl=3
diffus time=2 temp=900 dryo2 press=$partial_press
diffus time=13 temp=900 dryo2 press=$partial_press hcl=3
diffus time=10 temp=900 nitrogen hcl=3
diffus time=10 temp=800 t.final=800 nit hcl=3

# Extração do óxido de gate
extract name="gateox" thickness oxide mat.occno=1 x.val=0.005

##### AJUSTE DO Vt (dopagem do canal) ##########
implant amorphous phos dose=1.8e13 energy=25 pearson

depo poly thick=0.250 div=3 

etch poly left p1.x=0.35

# Relaxamento do mesh (importante para a formação dos spacers)
relax y.min=0.5


method fermi compress
diffuse time=5 temp=900 weto2 press=0.8

# Implante inicial
implant amorphous bf2 dose=2.0e14 energy=50 pearson

# Definição do spacer
set spacer=0.15
depo oxide thick=$"spacer" divisions=5
etch oxide dry thick=$"spacer"+0.005

# Implante P
implant amorphous bf2 dose=1.50e15 energy=60 pearson

# Annealing para difusão do implante p+
method fermi compress
diffuse time=5 temp=900 nitro press=1.0



# Contatos
etch oxide left p1.x=0.15
deposit alumin thick=0.03 div=2
etch alumin right p1.x=0.16


# Espelhamento da estrutura
structure mirror right



# Definição dos eletrodos
electrode name=gate x=0.5 
electrode name=source x=0
electrode name=drain x=1.1
electrode name=substrate backside


structure outfile=mos1ex08_0.str 

tonyplot  mos1ex08_0.str -set  mos1ex08_0.set


############# Medição do Vt ################

go atlas

models cvt srh print  
contact name=gate p.poly
interface qf=3e10

solve init


method  newton trap
solve prev

solve vdrain=-0.1 name=drain


log outf=mos1ex08_1.log master
solve vgate=0 vstep=-0.25 vfinal=-5.0 vdrain=-0.1 name=gate
save outf=mos1ex08_1.str

go atlas
extract init infile="mos1ex08_1.log"

extract name="pvt" (xintercept(maxslope(curve(abs(v."gate"),abs(i."drain")))) - abs(ave(v."drain"))/2.0)

tonyplot   mos1ex08_1.log -set mos1ex08_1_log.set

quit
